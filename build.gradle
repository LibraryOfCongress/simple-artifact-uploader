plugins {
  id "com.gradle.plugin-publish" version "0.9.4"
  id "com.github.kt3k.coveralls" version "2.6.3"
  id "java-gradle-plugin"
  id "eclipse"
  id "jacoco"
  id "findbugs"
  id "pmd"
}

repositories {
  jcenter()
}

version = "1.1.1"
group = "gov.loc.repository"

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.25',
  'org.apache.httpcomponents:httpclient:4.5.5',
  'org.apache.httpcomponents:httpmime:4.5.5',
  'com.google.code.gson:gson:2.8.5'
  compile gradleApi()

  testCompile 'junit:junit:4.12', 
  'org.mockito:mockito-core:2.19.0'
}

pluginBundle {
  website = 'https://github.com/libraryofcongress/simple-artifact-uploader'
  vcsUrl = 'https://github.com/libraryofcongress/simple-artifact-uploader'
  description = 'Upload artifacts to artifactory using its rest api instead of the provided plugin. '
  tags = ['artifactory', 'upload', 'rest-api']

  plugins {
    artifactory {
      id = 'gov.loc.repository.simple-artifact-uploader'
      displayName = 'Simple artifact uploader'
    }
  }
}

jacoco {
  toolVersion = "0.7.5.201505241946" //needed for jenkins to report code coverage correctly
}

findbugsTest.enabled = false

pmdTest.enabled = false

pmd {
  ruleSets = [
    "java-basic", 
    "java-braces",
    "java-clone",
    "java-codesize",
    "java-design",
    "java-empty",
    "java-finalizers",
    "java-imports",
    "java-j2ee",
    "java-javabeans",
    "java-optimizations",
    "java-strictexception",
    "java-strings",
    "java-sunsecure",
    "java-typeresolution",
    "java-unnecessary",
    "java-unusedcode"
  ]
}

jacocoTestReport {
  reports {
      xml.enabled = true // coveralls plugin depends on xml format report
      html.enabled = true
  }
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

//Keep this for easy viewing of html findbugs report
//tasks.withType(FindBugs) {
//  reports {
//      xml.enabled = false
//      html.enabled = true
//  }
//}
